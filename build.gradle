
plugins {
  id 'java'
  id 'eclipse'
  id 'maven'
  id 'jacoco'
  id 'pmd'
}


/*
 * Gets the version name from the latest Git tag of the form V-X.Y where X and Y are version numbers
 */
def getVersionName = { ->
  def stdout = new ByteArrayOutputStream()
  exec {
    commandLine 'git', 'describe', '--tags', '--match', 'V-*.*'
    standardOutput = stdout
  }
  def v = stdout.toString().trim() =~ /V-(\d+)\.(\d+)(?:-(\d+)-)?/
  return v[0][1] + "." + v[0][2] + "." + (v[0][3] == null ? 0 : v[0][3])
}


group = 'ch.kerbtier.hops-db'
version = getVersionName()


sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  compile 'com.mchange:c3p0:0.9.5.1'
  compile 'com.google.guava:guava:18.0'
  compile 'org.apache.commons:commons-lang3:3.4'
  
  compile 'ch.kerbtier.struwwel:struwwel:0.1.0'
  
  testCompile 'org.testng:testng:6.9.6'
  testCompile 'com.h2database:h2:1.4.187'
}

test {
  useTestNG()
  
  testLogging {
    exceptionFormat = 'full'
  }
  
  maxHeapSize = '8m'
}

jar {
  baseName = 'hops-db'
  version =  version
  manifest {
    attributes 'Implementation-Title': 'hops-db', 'Implementation-Version': version
  }
}

jacocoTestReport {
  reports {
    html.enabled true
  }
}
